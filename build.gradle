import java.time.Instant
import java.time.format.DateTimeFormatter
import java.time.temporal.ChronoUnit

buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        maven { url = "https://plugins.gradle.org/m2/"}
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath "gradle.plugin.com.github.johnrengelman:shadow:7.1.0"
    }
}

apply plugin: 'net.minecraftforge.gradle'
// Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply plugin: "com.github.johnrengelman.shadow"

version = '0.1.2'
group = 'info.tritusk.adventure'
archivesBaseName = 'adventure-platform-forge'

java.toolchain.languageVersion = JavaLanguageVersion.of(8)

println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))
minecraft {
    mappings channel: 'official', version: '1.16.5'

    runs {
        client {
            workingDirectory project.file('run_client')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'info'
            mods {
                adventure_platform_forge {
                    source sourceSets.main
                }
                adventure_platform_forge_test {
                    source sourceSets.test
                }
            }
        }
        server {
            workingDirectory project.file('run_server')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'info'
            mods {
                adventure_platform_forge {
                    source sourceSets.main
                }
                adventure_platform_forge_test {
                    source sourceSets.test
                }
            }
        }
        // There is no data to generate, so the runs.data was removed to keep things compact.
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    mavenCentral()
    maven {
        name = "sonatype-oss-snapshots"
        url = "https://oss.sonatype.org/content/repositories/snapshots/"
    }
}

configurations {
    shade
    implementation.extendsFrom shade
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.16.5-36.2.8'

    shade "net.kyori:adventure-api:4.10.1"
    shade "net.kyori:adventure-key:4.10.1"
    shade "net.kyori:adventure-platform-api:4.1.0"
    shade "net.kyori:adventure-text-serializer-plain:4.10.1"
    shade "net.kyori:adventure-text-serializer-gson:4.10.1"
}

shadowJar {
    configurations = [project.configurations.shade]
    relocate 'net.kyori.adventure', 'info.tritusk.adventure.platform.forge.lib.adventure'
}

artifacts {
    archives shadowJar
    shadow shadowJar
}

reobf {
    shadowJar {
        dependsOn createMcpToSrg
        mappings = createMcpToSrg.outputs.files.singleFile
    }
}


jar {
    manifest {
        attributes([
                "Specification-Title"     : "adventure",
                "Specification-Vendor"    : "KyoriPowered",
                "Specification-Version"   : "1",
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : "${version}",
                "Implementation-Vendor"   : "3TUSK",
                "Implementation-Timestamp": DateTimeFormatter.ISO_INSTANT.format(Instant.now().truncatedTo(ChronoUnit.SECONDS))
        ])
    }
}

jar.finalizedBy('reobfJar')


publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/fabrossmann/adventure-platform-forge")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
            }
        }
    }
    publications {
        release(MavenPublication) {
            groupId = "info.tritusk.adventure"
            artifactId = "adventure-platform-forge"

            artifact jar
            pom {
                name = 'adventure-platform-forge'
                description = 'Integration between adventure and Forge platform.'
                url = 'https://github.com/3TUSK/adventure-platform-forge'
                licenses {
                    license {
                        name = 'BSD-3-clause'
                        url = 'https://github.com/3TUSK/adventure-platform-forge/blob/bleeding/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = '3TUSK'
                        name = '3TUSK'
                    }
                    developer {
                        id = 'MojangPlsFix'
                        name = 'MojangPlsFix'
                    }
                }
                issueManagement {
                    system = 'GitHub Issues'
                    url = 'https://github.com/fabrossmann/adventure-platform-forge'
                }
            }
        }
    }
}
